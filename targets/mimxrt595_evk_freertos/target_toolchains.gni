
import("//build_overrides/pigweed.gni")

import("$dir_pigweed/targets/mimxrt595_evk_freertos/target_toolchains.gni")

_toolchain_args = {
  pw_third_party_freertos_CONFIG = "//targets/mimxrt595_evk_freertos:freertos_config"
  pw_third_party_freertos_PORT = "//targets/mimxrt595_evk_freertos:freertos_port"
  pw_boot_cortex_m_LINKER_SCRIPT = "//targets/mimxrt595_evk_freertos:flash_linker_script"
}

target_toolchain_mimxrt595_evk_freertos = {
  _excluded_members = [
    "defaults",
    "name",
  ]

  debug = {
    name = "mimxrt595_evk_freertos_debug"
    _toolchain_base = pw_target_toolchain_mimxrt595_evk_freertos.debug
    forward_variables_from(_toolchain_base, "*", _excluded_members)
    defaults = {
      forward_variables_from(_toolchain_base.defaults, "*")
      forward_variables_from(_toolchain_args, "*")
    }
  }

  speed_optimized = {
    name = "mimxrt595_evk_freertos_speed_optimized"
    _toolchain_base = pw_target_toolchain_mimxrt595_evk_freertos.speed_optimized
    forward_variables_from(_toolchain_base, "*", _excluded_members)
    defaults = {
      forward_variables_from(_toolchain_base.defaults, "*")
      forward_variables_from(_toolchain_args, "*")
    }
  }

  size_optimized = {
    name = "mimxrt595_evk_freertos_size_optimized"
    _toolchain_base = pw_target_toolchain_mimxrt595_evk_freertos.size_optimized
    forward_variables_from(_toolchain_base, "*", _excluded_members)
    defaults = {
      forward_variables_from(_toolchain_base.defaults, "*")
      forward_variables_from(_toolchain_args, "*")
    }
  }
}

# This list just contains the members of the above scope for convenience to make
# it trivial to generate all the toolchains in this file via a
# `generate_toolchains` target.
toolchains_list = [
  target_toolchain_mimxrt595_evk_freertos.debug,
  target_toolchain_mimxrt595_evk_freertos.speed_optimized,
  target_toolchain_mimxrt595_evk_freertos.size_optimized,
]
